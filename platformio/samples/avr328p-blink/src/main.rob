/*
 * Robcmp examples: Blink
 * Blinks the led on PIN13
 */

register int8 portb at 0x25 {
	bool pin8;
	bool pin9;
	bool pin10;
	bool pin11;
	bool pin12;
	bool pin13;
}

register int8 ddrb at 0x24 {
 	bool pin8;
	bool pin9;
	bool pin10;
	bool pin11;
	bool pin12;
	bool pin13;
}

void wait(int32 q);

int16 main() {
	arr = {1000000, 500000, 300000, 200000, 100000, 50000};

	/* pin 13 is controlled by bit 5 of DDRB and PORTB registers
	 * - bit 5 of DDRB indicates if the pin is input (0) or output (1)
	 * - bit 5 of PORTB toggles pin HIGH or LOW
	 */

	// set pin 13 as output
	ddrb.pin13 = true;

	i = 0;
	t = 5;
	loop {
		portb.pin13 = true;
		wait(arr[i]);

		portb.pin13 = false;
		wait(arr[i]);

		t--;
		if (t == 0) {
			t = 5;
			i++;
			if (i == 6) {
				i = 0;
			}
		}
	}
}
