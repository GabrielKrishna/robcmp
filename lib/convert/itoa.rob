/*
 * Part of the Robotics Language Standard Library, under the
 * Apache License v2.0.
 */

use math.abs;

const itoa_chars = "0123456789abcdef";

void itoa(int16 value, int8 base, char[] result) {
    if base < 2 or base > 16 {
        result[0] = 0;
        return;
    }

    negative = value < 0;
    value = abs16(value);
    
    i = 0;
    if value == 0 {
        result[i] = '0';
        i++;
    } else {
        while value != 0 {
            temp = value;
            value /= base;
            result[i] = itoa_chars[temp - value * base];
            i++;
        }
        if negative {
            result[i] = '-';
            i++;
        }
    }

    result[i] = 0;
    
    // reverse result
    j = 0;
    i--;
    while j < i {
        temp = result[j];
        result[j] = result[i];
        result[i] = temp;
        j++;
        i--;
    }
}
